header {
    #include "Emulator.h"
    #include <string>

    typedef const std::string& const_string_ref;
}

fsm Fsm(Application ctx) {
    after {
        self->ctx.updateMenu();
    }

    Start {
        loadCore(Emulator core) => CoreLoaded {
            if (!self->ctx.loadCore(core)) {
                forbid;
            }
        }

        quit() => Quit {
            if (!self->ctx.canQuit()) {
                forbid;
            }
        }
    }

    Quit {}

    CoreLoaded {
        loadGame(const_string_ref path) => GameRunning {
            if (!self->ctx.loadGame(path)) {
                forbid;
            }
        }

        unloadCore() => Start {
            if (!self->ctx.unloadCore()) {
                forbid;
            }
        }

        quit() => unloadCore() => quit();
    }

    GameRunning {
        resetGame() => GameRunning {
            self->ctx.resetGame();
        }

        turbo() => GameTurbo {
            if (self->ctx.hardcore()) {
                forbid;
            }
        }

        unloadGame() => CoreLoaded {
            if (!self->ctx.unloadGame()) {
                forbid;
            }
        }

        pauseGame() => GamePaused {
            if (self->ctx.hardcore()) {
                forbid;
            }

            self->ctx.pauseGame(true);
        }

        quit() => unloadGame() => unloadCore() => quit();
    }

    GamePaused {
        resumeGame() => GameRunning {
            self->ctx.pauseGame(false);
        }

        resetGame() => resumeGame() => resetGame();

        step() => FrameStep;

        turbo() => GameTurbo {
            if (self->ctx.hardcore()) {
                forbid;
            }
        }

        unloadGame() => CoreLoaded {
            if (!self->ctx.unloadGame()) {
                forbid;
            }
        }

        quit() => unloadGame() => unloadCore() => quit();
    }

    FrameStep {
        resumeGame() => GamePaused;
    }

    GameTurbo {
        resetGame() => normal() => resetGame();

        normal() => GameRunning;

        unloadGame() => CoreLoaded {
            if (!self->ctx.unloadGame()) {
                forbid;
            }
        }

        pauseGame() => GamePaused {
            if (self->ctx.hardcore()) {
                forbid;
            }

            self->ctx.pauseGame(true);
        }

        quit() => unloadGame() => unloadCore() => quit();
    }
}
